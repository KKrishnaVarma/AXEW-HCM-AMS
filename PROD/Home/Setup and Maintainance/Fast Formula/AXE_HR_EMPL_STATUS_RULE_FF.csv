FORMULA_ID,FORMULA_TEXT
300000036915605,"
/* ******************************************************************
 * Formula Name     : AXE_HR_EMPL_STATUS_RULE_FF
 * Rice ID          : HCM_I040
 * Formula Type     : Extract Rule
 * Description      : To determine the Effective Date of recent Assignment Status change for an empployee in Assignment Record.
 * Logic            : Compare the current Assignment Status with previous rows until there is a change identified.      
 *                    
 *
 
 * Change History
 * --------------
 *
 * Name 	                               Ver      Date         Description
 *-----------------------------------     ------   ------------ ------------
   Abu Thahir                             1.0	   2019-Jun-11  Initial Draft
***************************************************************************** */



DEFAULT FOR DATA_ELEMENTS IS EMPTY_TEXT_TEXT
DEFAULT FOR ELEMENT_REPORTING_NAME IS '0'
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_EFFECTIVE_END_DATE IS '4712/12/31 00:00:00' (date)
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_EFFECTIVE_START_DATE IS '1901/01/01 00:00:00' (date)
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_ASSIGNMENT_ID IS 0

DEFAULT FOR PER_ASG_ASSIGNMENT_STATUS_TYPE IS 'NODATA'
DEFAULT FOR PER_ASG_EFFECTIVE_START_DATE IS '1903/01/01 00:00:00' (date)

INPUTS ARE DATA_ELEMENTS (TEXT_TEXT)

TRACE_FLAG = 'N' 
L_ASG_END_DATE = '4712/12/31 00:00:00' (date)
L_ASG_STRT_DATE = '1902/01/01 00:00:00' (date)
L_EFF_DT = '1901/01/01 00:00:00' (date)
L_ASG_STATUS_CHG_DT = '1901/01/01 00:00:00' (date)
L_CURR_ASG_ID = 0
L_PERSON_ID = 0
S_ASSIGNMENT_ID  = 'NO DATA'
S_ASG_START_DATE  = 'NO DATA'

L_COUNTER = PER_HIST_ASG_ASSIGNMENT_ID.LAST(-1) 

IF TRACE_FLAG = 'Y' THEN (RET = ESS_LOG_WRITE(' AXE_HR_EMPL_STATUS_RULE_FF- Starts '))

IF DATA_ELEMENTS.EXISTS('Assignment_ID') AND DATA_ELEMENTS.EXISTS('Assignment_Start_Date') THEN
(
	S_PERSON_ID = DATA_ELEMENTS['Person_ID']
	S_ASSIGNMENT_ID = DATA_ELEMENTS['Assignment_ID']
	S_ASSIGNMENT_STATUS = DATA_ELEMENTS['Assignment_Status_Type']
	S_ASG_START_DATE = DATA_ELEMENTS['Assignment_Start_Date']
	L_ASG_STRT_DATE = TO_DATE(SUBSTR(S_ASG_START_DATE,1,10),'YYYY/MM/DD')
	L_ASG_STATUS_CHG_DT = TO_DATE(SUBSTR(S_ASG_START_DATE,1,10),'YYYY/MM/DD')
	L_CURR_ASG_ID = TO_NUMBER(S_ASSIGNMENT_ID)
	L_PERSON_ID = TO_NUMBER(S_PERSON_ID)
L_ASG_STATUS_CHG_DT_1 = ' '

IF TRACE_FLAG = 'Y' THEN (RET = ESS_LOG_WRITE(' S_PERSON_ID = '|| S_PERSON_ID || ', ' || ' S_ASSIGNMENT_ID = '|| S_ASSIGNMENT_ID|| ', ' || ' S_ASG_START_DATE = '|| S_ASG_START_DATE || ', '|| ' S_ASSIGNMENT_STATUS = '|| S_ASSIGNMENT_STATUS))
		

CHANGE_CONTEXTS(PERSON_ID = L_PERSON_ID)

(
WHILE (PER_HIST_ASG_ASSIGNMENT_ID.EXISTS(L_COUNTER)) LOOP
(
	L_HIST_ASS_ID = PER_HIST_ASG_ASSIGNMENT_ID[L_COUNTER]
	L_HIST_ASS_START = PER_HIST_ASG_EFFECTIVE_START_DATE[L_COUNTER]

	IF (PER_HIST_ASG_ASSIGNMENT_TYPE[L_COUNTER] = 'E' AND PER_HIST_ASG_EFFECTIVE_START_DATE[L_COUNTER] < L_ASG_STRT_DATE)THEN 
	(
	
	CHANGE_CONTEXTS(EFFECTIVE_DATE= L_HIST_ASS_START, HR_ASSIGNMENT_ID = L_HIST_ASS_ID) 
	(

	L_HIST_ASSIGNMENT_STATUS  = PER_ASG_ASSIGNMENT_STATUS_TYPE

 
		IF TRACE_FLAG = 'Y' THEN (RET = ESS_LOG_WRITE(' L_HIST_ASS_ID = '|| TO_CHAR(L_HIST_ASS_ID) || ', '||' L_HIST_ASSIGNMENT_STATUS = '|| L_HIST_ASSIGNMENT_STATUS )) 
		IF (L_HIST_ASSIGNMENT_STATUS <> S_ASSIGNMENT_STATUS) THEN
		(
		  EXIT
		)
		ELSE
		(
		  L_ASG_STATUS_CHG_DT_1 = TO_CHAR(PER_ASG_EFFECTIVE_START_DATE,'YYYYMMDD')
		  IF TRACE_FLAG = 'Y' THEN (RET = ESS_LOG_WRITE(' L_ASG_STATUS_CHG_DT_1 = '|| L_ASG_STATUS_CHG_DT_1))
		)
	)

	)
L_COUNTER = PER_HIST_ASG_ASSIGNMENT_Id.PRIOR(L_COUNTER, -1) 
)

)



IF L_ASG_STATUS_CHG_DT_1 <> ' ' THEN
(
RULE_VALUE =L_ASG_STATUS_CHG_DT_1
)
ELSE
(RULE_VALUE = TO_CHAR(L_ASG_STATUS_CHG_DT,'YYYYMMDD'))


IF TRACE_FLAG = 'Y' THEN (RET = ESS_LOG_WRITE(' Return Value = '|| RULE_VALUE))
IF TRACE_FLAG = 'Y' THEN (RET = ESS_LOG_WRITE(' AXE_HR_EMPL_STATUS_RULE_FF- Ends '))

RETURN RULE_VALUE

)"
