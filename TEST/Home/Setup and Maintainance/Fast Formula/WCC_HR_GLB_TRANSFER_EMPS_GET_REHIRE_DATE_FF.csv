FORMULA_ID,FORMULA_TEXT
300000171806283,"
/***************************************************************************************************
Formula Name: WCC_HR_GLB_TRANSFER_EMPS_GET_REHIRE_DATE_FF
Formula Type: Extract Rule
Description : Fetches Previous Rehire Date For Global Transfer Employees

Org/Developer                         Date         		Comments
----------------------------------------------------------------------------------------------------
Deloitte/Karthikeya Andey	       	22/02/2021   	Initial version
***************************************************************************************************/
DEFAULT FOR PER_ASG_EFFECTIVE_START_DATE IS '0001/01/01 00:00:00' (date)
DEFAULT FOR PER_ASG_EFFECTIVE_END_DATE IS '0001/01/01 00:00:00' (date)
DEFAULT FOR PER_ASG_REL_DATE_START IS '0001/01/01 00:00:00' (date)
DEFAULT FOR PER_ASG_ASSIGNMENT_ID IS -1
DEFAULT FOR PER_ASG_ASSIGNMENT_NUMBER IS ' '
DEFAULT FOR PER_ASG_ACTION_CODE IS ' '

DEFAULT_DATA_VALUE FOR PER_HIST_ASG_EFFECTIVE_START_DATE IS '0001/01/01 00:00:00' (date)
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_EFFECTIVE_END_DATE IS '0001/01/01 00:00:00' (date)
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_ASSIGNMENT_ID IS -1
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_EFFECTIVE_LATEST_CHANGE IS 'N'
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_PRIMARY_WORK_REL_FLAG IS 'N'
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_PRIMARY_ASG_FLAG IS 'N'
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_PRIMARY_FLAG IS 'N'
DEFAULT_DATA_VALUE FOR PER_HIST_ASG_ACTION_CODE IS ' '


l_person_id            = GET_CONTEXT(PERSON_ID,1)
l_assignment_id        = PER_ASG_ASSIGNMENT_ID
l_latest_hire_date	   = PER_ASG_REL_DATE_START
l_assignment_eff_date  = PER_ASG_EFFECTIVE_START_DATE

l_action_code          = ' '
l_global_transfer_flag = 'N'
l_previous_rehire_date = '1900/01/01 00:00:00' (date)
i                      = 1


CHANGE_CONTEXTS(HR_ASSIGNMENT_ID = l_assignment_id, EFFECTIVE_DATE = l_latest_hire_date)
(
	l_action_code = PER_ASG_ACTION_CODE
	
	IF(l_action_code = 'GLB_TRANSFER' OR l_action_code = 'GLB_TEMP_ASG') THEN
	(
		l_global_transfer_flag = 'Y'
		
		CHANGE_CONTEXTS(PERSON_ID = l_person_id)
		(
			i  = 1
			WHILE PER_HIST_ASG_EFFECTIVE_START_DATE.EXISTS(i)
			LOOP 
			(
				IF(PER_HIST_ASG_ACTION_CODE[i]               = 'REHIRE' 
				   AND PER_HIST_ASG_PRIMARY_WORK_REL_FLAG[i] = 'Y' 
				   AND PER_HIST_ASG_PRIMARY_ASG_FLAG[i]      = 'Y' 
				   AND PER_HIST_ASG_PRIMARY_FLAG[i]          = 'Y')
				THEN
				(
					IF (TO_CHAR(l_assignment_eff_date, 'YYYY/MM/DD')      >= TO_CHAR(PER_HIST_ASG_EFFECTIVE_START_DATE[i], 'YYYY/MM/DD')
						AND TO_CHAR(l_previous_rehire_date, 'YYYY/MM/DD') <= TO_CHAR(PER_HIST_ASG_EFFECTIVE_START_DATE[i], 'YYYY/MM/DD'))
					THEN
					(
						l_previous_rehire_date = PER_HIST_ASG_EFFECTIVE_START_DATE[i]
					)
				)			
				i=i+1
			)
		)
	)
	
)

IF	TO_CHAR(l_previous_rehire_date, 'YYYY/MM/DD') = '1900/01/01' THEN
	rule_value = TRIM(l_global_transfer_flag)
ELSE 
	rule_value = TRIM(l_global_transfer_flag)||TO_CHAR(l_previous_rehire_date)

RETURN rule_value"
