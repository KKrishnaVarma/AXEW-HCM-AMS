FORMULA_ID,FORMULA_TEXT
300000186303878,"
/***************************************************************************************************
Formula Name: WCC_HR_MANAGER_WORK_EMAIL_FF
Formula Type: Extract Rule
Description : Fetches Manager Work Email Address
Org/Developer                         Date         		Comments
----------------------------------------------------------------------------------------------------
Deloitte/Karthikeya Andey	       	9/29/2021   	Initial version
***************************************************************************************************/
DEFAULT FOR PER_ASG_MGR_MANAGER_ID IS -1
DEFAULT_DATA_VALUE FOR PER_EXT_EMAIL_EMAIL_TYPE IS ' ' 
DEFAULT_DATA_VALUE FOR PER_EXT_EMAIL_EMAIL_ADDRESS IS ' '
DEFAULT_DATA_VALUE FOR PER_EXT_EMAIL_EMAIL_ADDRESS_ID IS -1
DEFAULT_DATA_VALUE FOR PER_EXT_EMAIL_DATE_FROM IS '0001/01/01 00:00:00' (date)
DEFAULT_DATA_VALUE FOR PER_EXT_EMAIL_DATE_TO IS '0001/01/01 00:00:00' (date)

l_email				= ' '
l_effective_date 	= TRUNC(GET_CONTEXT(EFFECTIVE_DATE, '0001/01/01 00:00:00'(date)))
l_manager_id		= -1

IF PER_ASG_MGR_MANAGER_ID WAS NOT DEFAULTED
THEN 
(
	l_manager_id = PER_ASG_MGR_MANAGER_ID
)

CHANGE_CONTEXTS(PERSON_ID = l_manager_id, EFFECTIVE_DATE = l_effective_date)
(
	l_arr_index = PER_EXT_EMAIL_EMAIL_TYPE.FIRST(-1)
	WHILE PER_EXT_EMAIL_EMAIL_TYPE.EXISTS(l_arr_index)
	LOOP 
	(
		IF PER_EXT_EMAIL_EMAIL_TYPE[l_arr_index] = 'W1' 
		THEN
		(
			l_email = PER_EXT_EMAIL_EMAIL_ADDRESS[l_arr_index]
		)			
		l_arr_index = PER_EXT_EMAIL_EMAIL_TYPE.NEXT(l_arr_index, -1)
	)
)	


RULE_VALUE = TRIM(l_email)

RETURN RULE_VALUE"
